plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    // Analysis
    id 'org.sonarqube' version '6.1.0.5360'
    id 'jacoco'
    id 'net.ltgt.errorprone' version '4.3.0'
    id 'com.diffplug.spotless' version '7.2.1'
}

group = 'no.hvl.tk'
version = '2.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'no.hvl.tk:ruleGenerator:1'

    // XML-marshalling not part of jdk 11 anymore.
    // https://mvnrepository.com/artifact/jakarta.xml.bind/jakarta.xml.bind-api
    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '4.0.2'
    // https://mvnrepository.com/artifact/org.glassfish.jaxb/jaxb-runtime
    implementation('org.glassfish.jaxb:jaxb-runtime:4.0.5')
    // https://mvnrepository.com/artifact/jakarta.activation/jakarta.activation-api
    implementation group: 'jakarta.activation', name: 'jakarta.activation-api', version: '2.1.3'

    // Add pair class
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.20.0'

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Form data request for testing
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpmime
    testImplementation 'org.apache.httpcomponents:httpmime:4.5.14'
    // Guava
    // https://mvnrepository.com/artifact/com.google.guava/guava
    testImplementation 'com.google.guava:guava:33.4.8-jre'
    // toString of input streams.
    // https://mvnrepository.com/artifact/commons-io/commons-io
    testImplementation 'commons-io:commons-io:2.19.0'

    errorprone('com.google.errorprone:error_prone_core:2.16')
}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'timkraeuter_Groove_Rule_Generation_Server'
        property 'sonar.organization', 'timkraeuter'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

tasks.named('sonar').configure {
    dependsOn jacocoTestReport
}

spotless {
    java {
        googleJavaFormat()
        formatAnnotations()
    }
}