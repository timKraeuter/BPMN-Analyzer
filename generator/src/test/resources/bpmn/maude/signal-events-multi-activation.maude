load model-checker.maude .

--- Multiset implementation could be extracted as well.
fmod MSET is pr
    STRING .
    sorts NeMSet MSet .
    subsort String < NeMSet < MSet .

    op none : -> MSet [ctor] .
    op __ : MSet MSet -> MSet [ctor assoc comm id: none] .
    op __ : NeMSet MSet -> NeMSet [ctor ditto] .
    op __ : MSet NeMSet -> NeMSet [ctor ditto] .

    op contains : MSet String -> Bool .

    vars X Y : String .
    var S S1 : MSet .

    eq contains(none, X) = false .
    eq contains(X S, X) = true .
    ceq contains(Y S, X) = contains(S, X) if X =/= Y .
endfm

mod BPMN-EXECUTION is
    pr MSET .
    pr STRING .
    pr CONFIGURATION .

    sort ProcessState .
    subsort String < Oid .

    --- BPMNSystem
    op BPMNSystem : -> Cid [ctor] .
    op messages :_ : MSet -> Attribute [ctor] .
    op processes :_ : Configuration -> Attribute [ctor] .

    --- Processes
    op ProcessSnapshot : -> Cid [ctor] .
    op tokens :_ : MSet -> Attribute [ctor] .
    op signals :_ : MSet -> Attribute [ctor] .
    op subprocesses :_ : Configuration -> Attribute [ctor] .
    ops Running, Terminated : -> ProcessState [ctor] .
    op state :_ : ProcessState -> Attribute [ctor] .

    --- Auxiliary
    op signalAll : Configuration MSet -> Configuration .
    op signal : MSet MSet -> MSet .
    op terminate : Configuration -> Configuration .

    vars P P1 : String .
    vars T T1 : MSet . --- tokens
    vars SIG : MSet . --- signals
    vars S : Configuration . --- subprocesses
    vars STATE : ProcessState . --- state
    var PS : Configuration .

    eq signalAll(none, T) = none .
    eq signalAll(< P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : STATE > PS, T1) = < P : ProcessSnapshot | tokens : T, signals : (SIG signal(T, T1)), subprocesses : signalAll(S, T1), state : STATE > signalAll(PS, T1) .

    ceq signal(P T, T1) = (P + "_signal") signal(T, T1) if contains(T1, P) .
    eq signal(P T, T1) = signal(T, T1) [owise] .
    eq signal(none, T1) = none .

    eq terminate(none) = none .
    --- NOOP if already terminated
    eq terminate(< P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : Terminated >) = < P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : Terminated > .
    --- Terminate all subprocesses recursively
    eq terminate(< P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : STATE > PS) = < P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : terminate(S), state : Terminated > terminate(PS) .

    rl [naturalTerminate] :
    < P : ProcessSnapshot | tokens : none, signals : SIG, subprocesses : none, state : Running >
                            =>
    < P : ProcessSnapshot | tokens : none, signals : SIG, subprocesses : none, state : Terminated > .
endm

mod BPMN-EXECUTION-signal-events-multi-activation is
    pr BPMN-EXECUTION .

    --- Generated variables
    vars T : MSet . --- tokens
    vars SIG : MSet . --- signals
    vars M : MSet . --- messages
    vars S : Configuration . --- subprocesses
    vars P : Configuration . --- processes
    vars PS : Configuration . --- other processes

    --- Generated rules
    rl [start_p1_StartEvent_1] :  < "p1" : ProcessSnapshot | tokens : ("start_p1 (StartEvent_1)" T), signals : (SIG), subprocesses : (S), state : Running > => < "p1" : ProcessSnapshot | tokens : ("start_p1_S1_Throw_1 (Flow_09vy9fu)" T), signals : (none), subprocesses : (S), state : Running > .
    rl [S1_Throw_1_Event_17hujxw] :  < "p1" : ProcessSnapshot | tokens : ("start_p1_S1_Throw_1 (Flow_09vy9fu)" T), signals : (SIG), subprocesses : (S), state : Running > => < "p1" : ProcessSnapshot | tokens : ("S1_Throw_1_S1_Throw_2 (Flow_0leshl2)" T), signals : (SIG), subprocesses : (S), state : Running > < "p2" : ProcessSnapshot | tokens : ("S1_Catch_S2_Catch (Flow_17kxv86)"), signals : (none), subprocesses : (none), state : Running > .
    rl [S1_Throw_2_Event_1apmx9f] :  < "p1" : ProcessSnapshot | tokens : ("S1_Throw_1_S1_Throw_2 (Flow_0leshl2)" T), signals : (SIG), subprocesses : (S), state : Running > => < "p1" : ProcessSnapshot | tokens : ("S1_Throw_2_S2_Throw (Flow_15c0t96)" T), signals : (SIG), subprocesses : (S), state : Running > < "p2" : ProcessSnapshot | tokens : ("S1_Catch_S2_Catch (Flow_17kxv86)"), signals : (none), subprocesses : (none), state : Running > .
    rl [S2_Throw_Event_0mh8q9a] :  < "signal-events-multi-activation" : BPMNSystem | messages : (M), processes : (< "p1" : ProcessSnapshot | tokens : ("S1_Throw_2_S2_Throw (Flow_15c0t96)" T), signals : (SIG), subprocesses : (S), state : Running > PS) > => < "signal-events-multi-activation" : BPMNSystem | messages : (M), processes : (signalAll(< "p1" : ProcessSnapshot | tokens : (T), signals : (SIG), subprocesses : (S), state : Running > PS, "S1_Catch_S2_Catch (Flow_17kxv86)")) > .
    rl [S2_Catch_Event_1exk58v] :  < "p2" : ProcessSnapshot | tokens : ("S1_Catch_S2_Catch (Flow_17kxv86)" T), signals : ("S1_Catch_S2_Catch (Flow_17kxv86)_signal" SIG), subprocesses : (S), state : Running > => < "p2" : ProcessSnapshot | tokens : ("S2_Catch_end_p2 (Flow_03j40c3)" T), signals : (SIG), subprocesses : (S), state : Running > .
    rl [end_p2_Event_0xkx78c] :  < "p2" : ProcessSnapshot | tokens : ("S2_Catch_end_p2 (Flow_03j40c3)" T), signals : (SIG), subprocesses : (S), state : Running > => < "p2" : ProcessSnapshot | tokens : (T), signals : (none), subprocesses : (S), state : Running > .

    --- Start configuration which would be generated
    op init : -> Configuration .
    eq init = < "signal-events-multi-activation" : BPMNSystem | messages : (none), processes : (< "p1" : ProcessSnapshot | tokens : ("start_p1 (StartEvent_1)"), signals : (none), subprocesses : (none), state : Running >) > .
endm

mod BPMN-PREDS is
    pr BPMN-EXECUTION-signal-events-multi-activation .
    pr SATISFACTION .
    subsort Configuration < State .

    var C : Configuration .
    var P : Prop .
    var X Y : Oid .
    var T : MSet . --- tokens
    var SIG : MSet . --- signals
    var M : MSet . --- messages
    var T1 : NeMSet .
    var S : Configuration .
    var State : ProcessState .


    op allTerminated : -> Prop .
    eq < X : BPMNSystem | messages : M, processes : (< Y : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : Running > C) > |= allTerminated = false .
    eq < X : BPMNSystem | messages : M, processes : (C) > |= allTerminated = true [owise] .

    op unsafe : -> Prop .
    eq < X : BPMNSystem | messages : M, processes : (< Y : ProcessSnapshot | tokens : (T1 T1 T), signals : SIG, subprocesses : S, state : State > C) > |= unsafe = true .
    eq < X : BPMNSystem | messages : M, processes : (C) > |= unsafe = false [owise] .

    --- Generated atomic propositions
    --- no propositions
endm

mod BPMN-CHECK is
    pr BPMN-PREDS .
    pr MODEL-CHECKER .
    pr LTL-SIMPLIFIER .

    var X : Configuration .
endm

search init =>! X such that X |= allTerminated = true .
