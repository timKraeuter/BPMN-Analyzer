load model-checker.maude .

--- Multiset implementation could be extracted as well.
fmod MSET is
    pr STRING .
    pr NAT .
    sorts NeMSet MSet .
    subsort String < NeMSet < MSet .

    op none : -> MSet [ctor] .
    op __ : MSet MSet -> MSet [ctor assoc comm id: none] .
    op __ : NeMSet MSet -> NeMSet [ctor ditto] .
    op __ : MSet NeMSet -> NeMSet [ctor ditto] .

    op contains : MSet String -> Bool .

    var X : String .
    var Y : String .
    var S S1 : MSet .

    eq contains(none, X) = false .
    eq contains(X S, X) = true .
    ceq contains(Y S, X) = contains(S, X) if X =/= Y .

    op size : MSet -> Nat .
    eq size(none) = 0 .
    eq size(X S) = size(S) + 1 .

    op lessOrEqualSize : MSet MSet -> Bool .
    ceq lessOrEqualSize(S, S1) = true if size(S) <= size(S1) .
    eq lessOrEqualSize(S, S1) = false [owise] .
endfm

mod BPMN-EXECUTION is
    pr MSET .
    pr STRING .
    pr CONFIGURATION .

    sort ProcessState .
    subsort String < Oid .

    --- BPMNSystem
    op BPMNSystem : -> Cid [ctor] .
    op messages :_ : MSet -> Attribute [ctor] .
    op processes :_ : Configuration -> Attribute [ctor] .

    --- Processes
    op ProcessSnapshot : -> Cid [ctor] .
    op name :_ : String -> Attribute [ctor] .
    op tokens :_ : MSet -> Attribute [ctor] .
    op signals :_ : MSet -> Attribute [ctor] .
    op subprocesses :_ : Configuration -> Attribute [ctor] .
    ops Running, Terminated : -> ProcessState [ctor] .
    op state :_ : ProcessState -> Attribute [ctor] .

    --- Auxiliary
    op signalAll : Configuration MSet -> Configuration .
    op signal : MSet MSet -> MSet .
    op terminate : Configuration -> Configuration .

    var X : Oid .
    vars P P1 : String .
    vars T T1 : MSet . --- tokens
    vars SIG : MSet . --- signals
    vars S : Configuration . --- subprocesses
    vars STATE : ProcessState . --- state
    var PS : Configuration .

    eq signalAll(none, T) = none .
    ceq signalAll(< X : ProcessSnapshot | name : P, tokens : T, signals : SIG, subprocesses : S, state : STATE > PS, T1) = < X : ProcessSnapshot | name : P, tokens : T, signals : (SIG signal(T, T1)), subprocesses : signalAll(S, T1), state : STATE > signalAll(PS, T1) if lessOrEqualSize(SIG, T1). --- maximum signal bound is the number of tokens.
    eq signalAll(< X : ProcessSnapshot | name : P, tokens : T, signals : SIG, subprocesses : S, state : STATE > PS, T1) = < X : ProcessSnapshot | name : P, tokens : T, signals : (SIG), subprocesses : signalAll(S, T1), state : STATE > signalAll(PS, T1) [owise] .

    ceq signal(P T, T1) = (P + "_signal") signal(T, T1) if contains(T1, P) .
    eq signal(P T, T1) = signal(T, T1) [owise] .
    eq signal(none, T1) = none .

    eq terminate(none) = none .
    --- NOOP if already terminated
    eq terminate(< X : ProcessSnapshot | name : P, tokens : T, signals : SIG, subprocesses : S, state : Terminated >) = < X : ProcessSnapshot | name : P, tokens : T, signals : SIG, subprocesses : S, state : Terminated > .
    --- Terminate all subprocesses recursively
    eq terminate(< X : ProcessSnapshot | name : P, tokens : T, signals : SIG, subprocesses : S, state : STATE > PS) = < X : ProcessSnapshot | name : P, tokens : T, signals : SIG, subprocesses : terminate(S), state : Terminated > terminate(PS) .

    rl [naturalTerminate] :
    < X : ProcessSnapshot | name : P, tokens : none, signals : SIG, subprocesses : none, state : Running >
                            =>
    < X : ProcessSnapshot | name : P, tokens : none, signals : none, subprocesses : none, state : Terminated > .
endm

mod BPMN-EXECUTION-signal-events is
    pr BPMN-EXECUTION .

    --- Generated variables
    vars o0 : Oid . --- Oids
    vars T : MSet . --- tokens
    vars SIG : MSet . --- signals
    vars M : MSet . --- messages
    vars S : Configuration . --- subprocesses
    vars P : Configuration . --- processes
    vars PS : Configuration . --- other processes

    --- Generated rules
    rl [start_p1_StartEvent_1] :  < o0 : ProcessSnapshot | name : "p1", tokens : ("start_p1 (StartEvent_1)" T), signals : (SIG), subprocesses : (S), state : Running > => < o0 : ProcessSnapshot | name : "p1", tokens : ("start_p1_S1_Throw (Flow_09vy9fu)" T), signals : (none), subprocesses : (S), state : Running > .
    rl [S1_Throw_Event_17hujxw] :  < o0 : ProcessSnapshot | name : "p1", tokens : ("start_p1_S1_Throw (Flow_09vy9fu)" T), signals : (SIG), subprocesses : (S), state : Running > => < o0 : ProcessSnapshot | name : "p1", tokens : ("S1_Throw_S2_Catch (Flow_0leshl2)" T), signals : (SIG), subprocesses : (S), state : Running > < o0 : ProcessSnapshot | name : "p2", tokens : ("S1_Catch_S2_Throw (Flow_0ra23g1)"), signals : (none), subprocesses : (none), state : Running > < o0 : ProcessSnapshot | name : "p3", tokens : ("S1_Catch_EV_Gateway (Flow_0ei4onq)"), signals : (none), subprocesses : (none), state : Running > .
    rl [S2_Catch_Event_0yxokjx] :  < o0 : ProcessSnapshot | name : "p1", tokens : ("S1_Throw_S2_Catch (Flow_0leshl2)" T), signals : ("S1_Throw_S2_Catch (Flow_0leshl2)_signal" SIG), subprocesses : (S), state : Running > => < o0 : ProcessSnapshot | name : "p1", tokens : ("S2_Catch_end_p1 (Flow_07uqhhm)" T), signals : (SIG), subprocesses : (S), state : Running > .
    rl [end_p1_Event_0mh8q9a] :  < o0 : ProcessSnapshot | name : "p1", tokens : ("S2_Catch_end_p1 (Flow_07uqhhm)" T), signals : (SIG), subprocesses : (S), state : Running > => < o0 : ProcessSnapshot | name : "p1", tokens : (T), signals : (none), subprocesses : (S), state : Running > .
    rl [S2_Throw_Event_16dtcup] :  < "signal-events" : BPMNSystem | messages : (M), processes : (< o0 : ProcessSnapshot | name : "p2", tokens : ("S1_Catch_S2_Throw (Flow_0ra23g1)" T), signals : (SIG), subprocesses : (S), state : Running > PS) > => < "signal-events" : BPMNSystem | messages : (M), processes : (signalAll(< o0 : ProcessSnapshot | name : "p2", tokens : (T), signals : (SIG), subprocesses : (S), state : Running > PS, "S1_Throw_S2_Catch (Flow_0leshl2)" "EV_Gateway (Gateway_1ion030)")) > .
    rl [EV_Gateway_Gateway_1ion030] :  < o0 : ProcessSnapshot | name : "p3", tokens : ("S1_Catch_EV_Gateway (Flow_0ei4onq)" T), signals : (SIG), subprocesses : (S), state : Running > => < o0 : ProcessSnapshot | name : "p3", tokens : ("EV_Gateway (Gateway_1ion030)" T), signals : (none), subprocesses : (S), state : Running > .
    rl [S2_Catch_Event_0bwjswz] :  < o0 : ProcessSnapshot | name : "p3", tokens : ("EV_Gateway (Gateway_1ion030)" T), signals : ("EV_Gateway (Gateway_1ion030)_signal" SIG), subprocesses : (S), state : Running > => < o0 : ProcessSnapshot | name : "p3", tokens : ("S2_Catch_end_p3 (Flow_0yzxakb)" T), signals : (SIG), subprocesses : (S), state : Running > .
    rl [end_p3_Event_16f3ui0] :  < o0 : ProcessSnapshot | name : "p3", tokens : ("S2_Catch_end_p3 (Flow_0yzxakb)" T), signals : (SIG), subprocesses : (S), state : Running > => < o0 : ProcessSnapshot | name : "p3", tokens : (T), signals : (none), subprocesses : (S), state : Running > .

    --- Start configuration which would be generated
    op init : -> Configuration .
    eq init = < "signal-events" : BPMNSystem | messages : (none), processes : (< "0" : ProcessSnapshot | name : "p1", tokens : ("start_p1 (StartEvent_1)"), signals : (none), subprocesses : (none), state : Running >) > .
endm

mod BPMN-PREDS is
    pr BPMN-EXECUTION-signal-events .
    pr SATISFACTION .
    subsort Configuration < State .

    var C : Configuration .
    var P : Prop .
    var X Y : Oid .
    var N : String . --- name
    var T : MSet . --- tokens
    var SIG : MSet . --- signals
    var M : MSet . --- messages
    var T1 : NeMSet .
    var S : Configuration .
    var State : ProcessState .


    op allTerminated : -> Prop .
    eq < X : BPMNSystem | messages : M, processes : (< Y : ProcessSnapshot | name : N, tokens : T, signals : SIG, subprocesses : S, state : Running > C) > |= allTerminated = false .
    eq < X : BPMNSystem | messages : M, processes : (C) > |= allTerminated = true [owise] .

    op unsafe : -> Prop .
    eq < X : BPMNSystem | messages : M, processes : (< Y : ProcessSnapshot | name : N, tokens : (T1 T1 T), signals : SIG, subprocesses : S, state : State > C) > |= unsafe = true .
    eq < X : BPMNSystem | messages : M, processes : (C) > |= unsafe = false [owise] .

    --- Generated atomic propositions
    --- no propositions
endm

mod BPMN-CHECK is
    pr BPMN-PREDS .
    pr MODEL-CHECKER .
    pr LTL-SIMPLIFIER .

    var X : Configuration .
endm

search init =>! X such that X |= allTerminated = true .
