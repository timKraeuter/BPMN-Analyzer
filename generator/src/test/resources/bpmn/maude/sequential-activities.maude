load model-checker.maude .

--- Multiset implementation could be extracted as well.
fmod MSET is pr
    STRING .
    sorts NeMSet MSet .
    subsort String < NeMSet < MSet .

    op none : -> MSet [ctor] .
    op __ : MSet MSet -> MSet [ctor assoc comm id: none] .
    op __ : NeMSet MSet -> NeMSet [ctor ditto] .
    op __ : MSet NeMSet -> NeMSet [ctor ditto] .

    op contains : MSet String -> Bool .

    vars X Y : String .
    var S S1 : MSet .

    eq contains(none, X) = false .
    eq contains(X S, X) = true .
    ceq contains(Y S, X) = contains(S, X) if X =/= Y .
endfm

mod BPMN-EXECUTION is
    pr MSET .
    pr STRING .
    pr CONFIGURATION .

    sort ProcessState .
    subsort String < Oid .

    --- BPMNSystem
    op BPMNSystem : -> Cid [ctor] .
    op messages :_ : MSet -> Attribute [ctor] .
    op processes :_ : Configuration -> Attribute [ctor] .

    --- Processes
    op ProcessSnapshot : -> Cid [ctor] .
    op tokens :_ : MSet -> Attribute [ctor] .
    op signals :_ : MSet -> Attribute [ctor] .
    op subprocesses :_ : Configuration -> Attribute [ctor] .
    ops Running, Terminated : -> ProcessState [ctor] .
    op state :_ : ProcessState -> Attribute [ctor] .

    --- Auxiliary
    op signalAll : Configuration MSet -> Configuration .
    op signal : MSet MSet -> MSet .
    op terminate : Configuration -> Configuration .

    vars P P1 : String .
    vars T T1 : MSet . --- tokens
    vars SIG : MSet . --- signals
    vars S : Configuration . --- subprocesses
    vars STATE : ProcessState . --- state
    var PS : Configuration .

    eq signalAll(none, T) = none .
    eq signalAll(< P : ProcessSnapshot | tokens : T, subprocesses : S, state : STATE > PS, T1) = < P : ProcessSnapshot | tokens : signal(T, T1), subprocesses : S, state : STATE > signalAll(PS, T1) .

    ceq signal(P T, T1) = P (P + "_signal") signal(T, T1) if contains(T1, P) .
    eq signal(P T, T1) = P signal(T, T1) [owise] .
    eq signal(none, T1) = none .

    eq terminate(none) = none .
    --- NOOP if already terminated
    eq terminate(< P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : Terminated >) = < P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : Terminated > .
    --- Terminate all subprocesses recursively
    eq terminate(< P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : STATE > PS) = < P : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : terminate(S), state : Terminated > terminate(PS) .

    rl [naturalTerminate] :
    < P : ProcessSnapshot | tokens : none, signals : SIG, subprocesses : none, state : Running >
                            =>
    < P : ProcessSnapshot | tokens : none, signals : SIG, subprocesses : none, state : Terminated > .
endm

mod BPMN-EXECUTION-sequential-activities is
    pr BPMN-EXECUTION .

    --- Generated variables
    vars T : MSet . --- tokens
    vars SIG : MSet . --- signals
    vars M : MSet . --- messages
    vars S : Configuration . --- subprocesses
    vars P : Configuration . --- processes

    --- Generated rules
    rl [start_StartEvent_1] :  < "sequential-activities" : ProcessSnapshot | tokens : ("start (StartEvent_1)" T), signals : (SIG), subprocesses : (S), state : Running > => < "sequential-activities" : ProcessSnapshot | tokens : ("start_A (Flow_1u1u0cf)" T), signals : (none), subprocesses : (S), state : Running > .
    rl [A_Activity_1hogwa8_start] :  < "sequential-activities" : ProcessSnapshot | tokens : ("start_A (Flow_1u1u0cf)" T), signals : (SIG), subprocesses : (S), state : Running > => < "sequential-activities" : ProcessSnapshot | tokens : ("A (Activity_1hogwa8)" T), signals : (none), subprocesses : (S), state : Running > .
    rl [A_Activity_1hogwa8_end] :  < "sequential-activities" : ProcessSnapshot | tokens : ("A (Activity_1hogwa8)" T), signals : (SIG), subprocesses : (S), state : Running > => < "sequential-activities" : ProcessSnapshot | tokens : ("A_B (Flow_1b2a4a3)" T), signals : (none), subprocesses : (S), state : Running > .
    rl [B_Activity_02rpmxb_start] :  < "sequential-activities" : ProcessSnapshot | tokens : ("A_B (Flow_1b2a4a3)" T), signals : (SIG), subprocesses : (S), state : Running > => < "sequential-activities" : ProcessSnapshot | tokens : ("B (Activity_02rpmxb)" T), signals : (none), subprocesses : (S), state : Running > .
    rl [B_Activity_02rpmxb_end] :  < "sequential-activities" : ProcessSnapshot | tokens : ("B (Activity_02rpmxb)" T), signals : (SIG), subprocesses : (S), state : Running > => < "sequential-activities" : ProcessSnapshot | tokens : ("B_end (Flow_1tj96s9)" T), signals : (none), subprocesses : (S), state : Running > .
    rl [end_Event_1xnuloc] :  < "sequential-activities" : ProcessSnapshot | tokens : ("B_end (Flow_1tj96s9)" T), signals : (SIG), subprocesses : (S), state : Running > => < "sequential-activities" : ProcessSnapshot | tokens : (T), signals : (none), subprocesses : (S), state : Running > .

    --- Start configuration which would be generated
    op init : -> Configuration .
    eq init = < "sequential-activities" : BPMNSystem | messages : (none), processes : (< "sequential-activities" : ProcessSnapshot | tokens : ("start (StartEvent_1)"), signals : (none), subprocesses : (none), state : Running >) > .
endm

mod BPMN-PREDS is
    pr BPMN-EXECUTION-sequential-activities .
    pr SATISFACTION .
    subsort Configuration < State .

    var C : Configuration .
    var P : Prop .
    var X Y : Oid .
    var T : MSet . --- tokens
    var SIG : MSet . --- signals
    var M : MSet . --- messages
    var T1 : NeMSet .
    var S : Configuration .
    var State : ProcessState .


    op allTerminated : -> Prop .
    eq < X : BPMNSystem | messages : M, processes : (< Y : ProcessSnapshot | tokens : T, signals : SIG, subprocesses : S, state : Running > C) > |= allTerminated = false .
    eq < X : BPMNSystem | messages : M, processes : (C) > |= allTerminated = true [owise] .

    op unsafe : -> Prop .
    eq < X : BPMNSystem | messages : M, processes : (< Y : ProcessSnapshot | tokens : (T1 T1 T), signals : SIG, subprocesses : S, state : State > C) > |= unsafe = true .
    eq < X : BPMNSystem | messages : M, processes : (C) > |= unsafe = false [owise] .

    --- Generated atomic propositions
    --- no propositions
endm

mod BPMN-CHECK is
    pr BPMN-PREDS .
    pr MODEL-CHECKER .
    pr LTL-SIMPLIFIER .

    var X : Configuration .
endm

red modelCheck(init,  <> [] allTerminated) .
